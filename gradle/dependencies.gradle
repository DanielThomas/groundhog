/*
 * Copyright 2013-2014 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

ext {
  libraries = [:]
  versions = [:]
}

versions.guava = '17.0'
versions.jetty = '9.1.4.v20140401'
versions.hc = '4.3.3'
versions.slf4j = '1.7.7'
versions.spock = '0.7-groovy-2.0'

configurations.all {
  resolutionStrategy.eachDependency { details ->
    switch (details.requested.name) {
      case 'junit':
      case 'junit-dep':
        details.useTarget 'junit:junit:4.11'
        break
      case 'slf4j-api':
        details.useVersion versions.slf4j
        break
      case 'log4j':
        details.useTarget "org.slf4j:log4j-over-slf4j:$versions.slf4j"
        break
      case 'commons-logging':
        details.useTarget "org.slf4j:jcl-over-slf4j:$versions.slf4j"
        break
      case 'groovy-all':
        details.useVersion '2.2.2'
        break
    }
  }
}


libraries.guava = "com.google.guava:guava:$versions.guava"
libraries.guice = 'com.google.inject:guice:3.0'
libraries.logback = 'ch.qos.logback:logback-classic:1.1.2'
libraries.jsr305 = 'com.google.code.findbugs:jsr305:2.0.3'
libraries.inject = 'javax.inject:javax.inject:1'

libraries.netty = [
    'io.netty:netty-all:4.0.18.Final',
    'org.javassist:javassist:3.18.1-GA',
    'com.jcraft:jzlib:1.1.3'
]

libraries.jackson = 'com.fasterxml.jackson.core:jackson-core:2.3.2'
libraries.jsoup = 'org.jsoup:jsoup:1.7.3'

libraries.jettyserver = "org.eclipse.jetty:jetty-server:$versions.jetty"
libraries.jettyservlet = "org.eclipse.jetty:jetty-servlet:$versions.jetty"
libraries.jettyclient = "org.eclipse.jetty:jetty-client:$versions.jetty"
libraries.httpclient = "org.apache.httpcomponents:httpclient:$versions.hc"
libraries.httpmime = "org.apache.httpcomponents:httpmime:$versions.hc"

libraries.httptest = [
    libraries.jettyserver,
    libraries.jettyclient,
    libraries.jettyservlet,
    libraries.httpclient,
    libraries.httpmime
]

libraries.jmeter = 'org.apache.jmeter:ApacheJMeter_components:2.11'

libraries.littleproxy = dependencies.create('org.littleshoot:littleproxy:1.0.0-beta8-SNAPSHOT') {
  exclude module: 'slf4j-log4j12'
}

libraries.guavatest = "com.google.guava:guava-testlib:$versions.guava"
libraries.spock = [
    "org.spockframework:spock-core:$versions.spock",
    "org.spockframework:spock-guice:$versions.spock",
    'cglib:cglib-nodep:3.1',
    'org.objenesis:objenesis:2.1'
]

libraries.testlibs = [
    libraries.guavatest,
    libraries.spock
]
